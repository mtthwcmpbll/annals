buildscript {
    ext {
        kotlinVersion = '1.2.51'
        springBootVersion = '2.1.0.M1'
        springCloudContractVersion = '2.0.1.RELEASE'
    }
    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.springframework.cloud:spring-cloud-contract-gradle-plugin:${springCloudContractVersion}")

        classpath "com.netflix.nebula:nebula-publishing-plugin:8.1.0"
        classpath "com.netflix.nebula:nebula-project-plugin:4.1.0"
    }
}

apply plugin: "kotlin"
apply plugin: "kotlin-spring"
apply plugin: "eclipse"

apply plugin: "org.springframework.boot"
apply plugin: "io.spring.dependency-management"
apply plugin: "spring-cloud-contract"

apply plugin: "nebula.maven-publish"
apply plugin: "nebula.project"

apply from: "${rootDir}/gradle/react-client.gradle"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-contract-dependencies:${springCloudContractVersion}"
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.2'

    compile("org.jetbrains.kotlin:kotlin-stdlib-jre8")
    compile("org.jetbrains.kotlin:kotlin-reflect")

    compileOnly("org.springframework.boot:spring-boot-devtools")
	compile('org.springframework.boot:spring-boot-starter-actuator')
	compile('org.springframework.boot:spring-boot-starter-data-mongodb-reactive')
	compile('org.springframework.boot:spring-boot-starter-webflux')

    compile("com.fasterxml.jackson.datatype:jackson-datatype-jsr310")

    // BEGIN JDK11 dependency fix (removed Unsafe.defineClass)
    compile('net.bytebuddy:byte-buddy:1.8.17')
    // END JDK11 dependencies

    compile('de.flapdoodle.embed:de.flapdoodle.embed.mongo:2.1.1')
	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('io.projectreactor:reactor-test')
    testCompile("org.springframework.cloud:spring-cloud-starter-contract-verifier")
}

publishing {
    repositories {
        maven {
            url "gcs://repo.snowfort.com/${project.version.toString().endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'}"
        }
    }
    publications {
        service(MavenPublication) {
            from components.java
        }
    }
}

//tasks.release.finalizedBy tasks.publish

contracts {
    basePackageForTests = 'com.snowfort.annals.entry.contract'
    packageWithBaseClasses = 'com.snowfort.annals.entry.contract'
    testMode = 'EXPLICIT'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}